sarosEclipse {
  manifest = file('META-INF/MANIFEST.MF')
  excludeManifestDependencies = ['org.junit', 'saros.eclipse', 'saros.core']
  addDependencies = true
}

configurations {
    stfTest
    stfFlakyAndFailingTest
}

sourceSets {
    stfTest {
        java.srcDir 'test'
    }
    stfFlakyAndFailingTest {
        java.srcDir 'test'
    }
}

dependencies {
    stfTestCompile project(':saros.stf')
    stfTestCompile project(path: ':saros.stf', configuration: 'testing')
    stfTestCompile configurations.testConfig
    // TODO: The custom saros eclipse plugin adds all dependencies to implementation
    stfTestCompile configurations.implementation
    stfFlakyAndFailingTest configurations.stfTestCompile
    testCompile configurations.stfTestCompile // for running the tests in eclipse
}

class StfTest extends Test {
  StfTest() {
    this.setGroup('Verification')
    this.setDescription('Runs the stf tests. Requires a corresponding test environment')
    this.setTestClassesDirs(this.getProject().getSourceSets().getByName('stfTest').getRuntimeClasspath())

    this.testLogging{ loggingContainer ->
        loggingContainer.setShowStandardStreams(true)
        loggingContainer.setEvents(['started', 'passed', 'skipped', 'failed', 'standardOut', 'standardError'])
    }

    List<String> failedTests = new ArrayList<String>();
    this.afterTest { descriptor, result ->
        if( result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE ){
            ext.failedStfTests << "${descriptor.className}.${descriptor.name}"
        }
    }

    this.afterSuite { suite, result ->
        if ( !suite.parent && !ext.failedStfTests.isEmpty() ) {
            logger.lifecycle("Failed tests:")
            ext.failedStfTests.each { logger.lifecycle(it) }
        }
    }
  }
}

task stfTest (type: StfTest, dependsOn: [':saros.eclipse:build']) {
    include '**/stf/test/**/*Test.*'
    exclude '**/whiteboard/**'

    useJUnit {
        excludeCategories 'saros.stf.test.categories.FlakyTests', 'saros.stf.test.categories.FailingTests'
    }
}

task stfFlakyAndFailingTest (type: StfTest, dependsOn: [':saros.eclipse:build']) {
    ignoreFailures = true
    useJUnit {
        includeCategories 'saros.stf.test.categories.FlakyTests', 'saros.stf.test.categories.FailingTests'
    }
}